subprojects {
  group 'uk.gov.bis.lite'

  apply plugin: 'java'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
  }

  ext.dropwizardVersion = '1.2.2'

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
  }

  def nexusUsername = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
  def nexusPassword = project.hasProperty('nexusPassword') ? project.nexusPassword : ''

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }

        artifact javadocJar {
          classifier "javadoc"
        }
      }
    }

    repositories {
      maven {
        name "snapshots"
        url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
        credentials {
          username nexusUsername
          password nexusPassword
        }
      }
      maven {
        name "releases"
        url "https://nexus.ci.uktrade.io/repository/maven-releases/"
        credentials {
          username nexusUsername
          password nexusPassword
        }
      }
    }
  }

  task javadocJar(type: Jar) {
    from javadoc
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }
}